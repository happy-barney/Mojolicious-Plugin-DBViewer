<%
  use Data::Page;
  use Mojo::JSON;
  
  # Command
  my $command = $dbviewer->command;

  # Validation
  my $params = $command->params($self);
  my $operators = [
    '',
    'contains',
    'like',
    '=',
    '<>',
    '<',
    '<=',
    '>',,
    '>=',
    'is null',
    'is not null',
    'is space',
    'is not space'
  ];
  my $rule = [
    database => {default => ''} => [
      'safety_name'
    ],
    table => {default => ''} => [
      'safety_name'
    ],
    condition_column => [
      'safety_name || blank'
    ],
    condition_value => [
      'not_blank'
    ],
    operator => [
      {in_array => $operators}
    ],
    'sort-key' => [
      'safety_name'
    ]
  ];
  my $vresult = $dbviewer->validator->validate($params, $rule);
  my $database = $vresult->data->{database};
  my $table = $vresult->data->{table};
  
  # DBI
  my $dbi = $dbviewer->dbi;
  
  # Paramters
  my $data = $vresult->data;
  my $column = $data->{condition_column};
  my $operator = $data->{operator};
  my $value = $data->{condition_value};
  
  my $db_value;
  my $where;
  if (defined $column && $column ne '' && defined $operator && $operator ne '') {
    # Where
    $where = $dbi->where;
    my $db_operator;
    if ($operator eq 'contains') {
      $db_operator = 'like';
      $db_value = "%$value%";
      $where->clause(":${column}{$db_operator}");
      $where->param({$column => $db_value});
    }
    elsif ($operator eq 'is null' || $operator eq 'is not null') {
      $where->clause("$column $operator");
    }
    elsif ($operator eq 'is space' || $operator eq 'is not space') {
      $db_operator = $operator eq 'is space' ? '=' : '<>';
      $db_value = '';
      $where->clause(":${column}{$db_operator}");
      $where->param({$column => $db_value});
    }
    else {
      $db_operator = $operator;
      $db_value = $value;
      $where->clause(":${column}{$db_operator}");
      $where->param({$column => $db_value});
    }
  }
  
  # Order by
  my $sort_key = $data->{'sort-key'};
  my $order_by = '';
  if (defined $sort_key) {
    $order_by = "order by $sort_key";
    
    my $order = param('order') || '';
    if ($order eq 'asc' || $order eq 'desc') {
      $order_by .= " $order";
    }
  }

  # Limit
  my $page = param('page') || 1;
  my $count = 100;
  my $offset = ($page - 1) * $count;
  
  # Get null allowed columns
  my $result = $dbi->select(
    table => "$database.$table",
    where => $where,
    append => "$order_by limit $offset, $count"
  );
  my $header = $result->header;
  my $rows = $result->fetch_all;
  my $sql = $dbi->last_sql;
  
  # Pager
  my $total = $dbi->select(
    'count(*)',
    table => "$database.$table",
    where => $where
  )->value;
  my $pager = Data::Page->new($total, $count, $page);
  
  # Global variable for included templates
  stash(pager => $pager);
  
  my $output = param('output') || '';
  if ($output eq 'json') {
    $self->render_json;
    return Mojo::JSON->new->encode({header => $header, rows => $rows});
  }
%>

% layout 'dbviewer_common', title => "Select * from $table limit 0, 1000";

  <ul class="nav nav-pills">
    <li class="active">
      <a href="<%= url_for("/dbviewer/tables?database=$database") %>"><%= $database %></a>
    </li>
    <li class="active">
      <a href="#"><%= $table %></a>
    </li>
  </ul>
  <h2>Select</h2>
  <form method="get" action="<%= url_for %>" >
    <div class="label">Condition</div>
    % my $soperators = [@$operators];
    % splice @$soperators, 0, 1, ['---' => ''];
    
    <div class="form-inline">
      <%= select_field condition_column => [['---' => ''], @$header], style => "width:250px" %>
      <%= select_field operator => $soperators, style => "width:130px" %>
      <%= input_tag 'condition_value', style => "width:470px" %>
      <%= submit_button 'Search', class => 'btn' %>
      <%= hidden_field database => param('database') %>
      <%= hidden_field table => param('table') %>
    </div>
  </form>
  <div class="well">
    <div><b>Query</b></div>
    <div><%= $sql %></div>
    % if (defined $db_value) {
      <div><b>Parameter:</b> <%= $db_value eq '' ? '(Space)' : $db_value %></div>
    % }
  </div>
  <div>
    % my $first_count = ($pager->current_page - 1) * $pager->entries_per_page + 1;
    % my $last_count = $first_count + $pager->entries_on_this_page - 1;
    <%= $first_count %> to <%= $last_count %> (Total <i><%= $pager->total_entries %></i>)
  </div>
  <table class="table table-bordered">
    <tr class="success">
      % for (my $i = 0; $i < @$header; $i++) {
        % my $h = $header->[$i];
        <th class="header--<%= $h %>"><%= $h %></th>
      % }
    </tr>
    % for (my $i = 0; $i < @$rows; $i++) {
      % my $row = $rows->[$i];
      <tr class="row--<%= $i + 1 %>">
        % for (my $k = 0; $k < @$row; $k++) {
          % my $data = $row->[$k];
          % my $h = $header->[$k];
          <td class="column--<%= $h %>"><%= $data %></td>
        % }
      </tr>
    % }
  </table>

  %= include "/dbviewer/page_navi";
